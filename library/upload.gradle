apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def GROUP_ID = 'com.github.Sum-sdl'
def ARTIFACT_ID = 'AndriodDevelopLibrary'

def siteUrl = 'https://github.com/Sum-sdl/AndriodDevelopLibrary' // 项目的主页
def gitUrl = 'https://github.com/Sum-sdl/AndriodDevelopLibrary.git' // Github仓库的url

group = GROUP_ID
archivesBaseName = ARTIFACT_ID

//./gradlew install 本地构建
//./gradlew bintrayUpload 上传代码到远程仓库
//JCenter远程仓库版本
version = "2.3.1"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            artifactId = ARTIFACT_ID

            project {
                packaging 'aar'
                // Add your description here
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'sum-sdl'    //填写的一些基本信息
                        name 'sdl'
                        email '1079569023@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
//    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "AndroidDevLibrary"
        name = ARTIFACT_ID    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }

}